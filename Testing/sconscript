# This file is licensed under the MIT License.
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.

"""
Sconcript for building different test modules.
"""

# python
import os
import sys
import platform

# scons
from SCons.Environment import Environment
from SCons.Script import SConscript
from SCons.Script.SConscript import Return

# project
from BuildUtils import ImportVar

MAIN_ENV = ImportVar('MAIN_ENV')

CXXTEST_DIR = MAIN_ENV['PROJECT_DIR'] + '/Dependencies/cxxtest'

TEST_ENV = Environment(
    DEBUG_BUILD=MAIN_ENV['DEBUG_BUILD'],
    CXXTEST=CXXTEST_DIR + '/bin/cxxtestgen',
    CXXTEST_PYTHON=sys.executable,
    toolpath=[CXXTEST_DIR + '/build_tools/SCons'],
    tools=['default', 'cxxtest']
)

TEST_ENV['PROJECT_DIR'] = MAIN_ENV['PROJECT_DIR']
TEST_ENV['BUILD_DIR'] = MAIN_ENV['BUILD_DIR']

if "windows" in platform.system().lower():
    DEBUG_DEFINE = 'NDEBUG'
    DEBUG_FLAG = "/Z7"
    DEBUG_OPT = "/O2"
    DEBUG_LINK = '/DEBUG:NONE'
    DEBUG_RUNTIME = "/MD"

    if TEST_ENV['DEBUG_BUILD']:
        DEBUG_DEFINE = 'DEBUG'
        DEBUG_OPT = "/Od"
        DEBUG_FLAG = "/Z7"
        DEBUG_LINK = '/DEBUG:FULL'
        DEBUG_RUNTIME = "/MDd"

else:
    DEBUG_DEFINE = 'NDEBUG'
    DEBUG_FLAG = "-g"
    DEBUG_OPT = "-O2"
    DEBUG_LINK = ''
    DEBUG_RUNTIME = ""

if "windows" in platform.system().lower():
    TEST_ENV.Append(
        CPPDEFINES=['NOMINMAX', "WIN32", DEBUG_DEFINE],
        CCFLAGS=[
            "/analyze-",
            "/GS",
            "/Zc:wchar_t",
            "/W1",
            DEBUG_FLAG,
            "/Gm-",
            DEBUG_OPT,
            "/WX-",
            '/FC',
            "/Zc:forScope",  # Force Conformance in for Loop Scope
            "/GR",           # Enable Run-Time Type Information
            "/Oy-",          # Disable Frame-Pointer Omission
            DEBUG_RUNTIME,
            "/EHsc",
            "/nologo",
        ],
        LINKFLAGS=[DEBUG_LINK])
else:
    TEST_ENV.Append(CCFLAGS=[
        "-std=c++11",
        DEBUG_FLAG,
    ])

TEST_ENV.Append(
    CPPPATH=[
        MAIN_ENV['PROJECT_DIR'] + "/build/include",
        MAIN_ENV['PROJECT_DIR'] + "/Testing",
        MAIN_ENV['PROJECT_DIR'] + "/Dependencies/glm",
        MAIN_ENV['PROJECT_DIR'] + "/Dependencies/cxxtest/cxxtest",
    ],
    CPPDEFINES=[
        "GLM_FORCE_RADIANS",
    ],
    LIBPATH=[
        MAIN_ENV['PROJECT_DIR'] + "/build/lib",
    ]
)

if "windows" in platform.system().lower():
    TEST_ENV.Append(LIBS=[
        "OpenDoorGL",
        'OpenGL32',
        'user32',
        'glew32'
    ])
else:
    TEST_ENV.Append(LIBS=[
        "OpenDoorGL",
        "GLEW",
    ])

TESTS = []
for root, dirs, files in os.walk("./"):
    for file in files:
        if file.endswith("sconscript"):
            if root == "./":
                continue
            TESTS.append(
                SConscript(
                    os.path.join(root, file),
                    duplicate=0,
                    exports=['TEST_ENV', 'MAIN_ENV']
                )
            )

Return('TESTS')
